
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РеквизитыОбъектаМетаданных.Очистить();
	
	ИмяМетаданных = Параметры.ИмяМетаданных;
	Имя = Параметры.Имя;
	
	
	Если ИмяМетаданных = "Справочники" ИЛИ ИмяМетаданных = "Документы"
			ИЛИ ИмяМетаданных = "БизнесПроцессы" ИЛИ ИмяМетаданных = "Задачи" Тогда

		// Реквизиты
		Для Каждого РеквизитОбъектаМетаданных Из Метаданные[ИмяМетаданных][Имя].Реквизиты Цикл

			НовСтр = РеквизитыОбъектаМетаданных.Добавить();
			НовСтр.Имя = РеквизитОбъектаМетаданных.Имя;
			НовСтр.ТипРеквизита = 3;

		КонецЦикла;

		// Стандартные реквизиты
		Для Каждого РеквизитОбъектаМетаданных Из Метаданные[ИмяМетаданных][Имя].СтандартныеРеквизиты Цикл

			НовСтр = РеквизитыОбъектаМетаданных.Добавить();
			НовСтр.Имя = РеквизитОбъектаМетаданных.Имя;
			НовСтр.ТипРеквизита = 1;

		КонецЦикла;

		// Табличные части
		Для Каждого ТабличнаяЧастьОбъектаМетаданных Из Метаданные[ИмяМетаданных][Имя].ТабличныеЧасти Цикл

			Для Каждого РеквизитОбъектаМетаданных Из ТабличнаяЧастьОбъектаМетаданных.Реквизиты Цикл

				НовСтр = РеквизитыОбъектаМетаданных.Добавить();
				НовСтр.Имя = ТабличнаяЧастьОбъектаМетаданных.Имя + "."
					+ РеквизитОбъектаМетаданных.Имя;
				НовСтр.ТипРеквизита = 2;

			КонецЦикла;

		КонецЦикла;

	КонецЕсли;
	
	Если ИмяМетаданных = "РегистрыСведений" Тогда

		// Измерения
		Для Каждого ИзмерениеОбъектаМетаданных Из Метаданные[ИмяМетаданных][Имя].Измерения Цикл

			НовСтр = РеквизитыОбъектаМетаданных.Добавить();
			НовСтр.Имя = ИзмерениеОбъектаМетаданных.Имя;
			НовСтр.ТипРеквизита = 3;

		КонецЦикла;

		// Ресурсы
		Для Каждого РесурсОбъектаМетаданных Из Метаданные[ИмяМетаданных][Имя].Ресурсы Цикл

			НовСтр = РеквизитыОбъектаМетаданных.Добавить();
			НовСтр.Имя = РесурсОбъектаМетаданных.Имя;
			НовСтр.ТипРеквизита = 3;

		КонецЦикла;

	КонецЕсли;
	
	
	НастройкиИсторииДанныхПредприятие = ИсторияДанных.ПолучитьНастройки(Метаданные[ИмяМетаданных][Имя]);
	
	Для Каждого РеквизитОбъектаМетаданных Из РеквизитыОбъектаМетаданных Цикл
		
		Значение = НастройкиИсторииДанныхПредприятие.ИспользованиеПолей.Получить(РеквизитОбъектаМетаданных.Имя);
		
		Если Значение = Неопределено Тогда
			РеквизитОбъектаМетаданных.ИсторияДанных = "Из настроек родителя";
		Иначе
			РеквизитОбъектаМетаданных.ИсторияДанных = Значение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиНаСервере()
	
	НИД = Новый НастройкиИсторииДанных;
	НИД.Использование = Истина;
	Для Каждого Стр Из РеквизитыОбъектаМетаданных Цикл
		
		НИД.ИспользованиеПолей.Вставить(Стр.Имя, Стр.ИсторияДанных);
		
	КонецЦикла;
	
	ИсторияДанных.УстановитьНастройки(Метаданные[ИмяМетаданных][Имя], НИД);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройки(Команда)
	УстановитьНастройкиНаСервере();
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовФормы

#КонецОбласти



#Область ОбработчикиКомандФормы

#КонецОбласти



#Область СлужебныеПроцедурыИФункции

#КонецОбласти
